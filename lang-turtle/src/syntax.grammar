@top TurtleDoc { (statement '.'*)* }                                            /* [1] 	turtleDoc 	::= 	statement* */

@skip { space | LineComment }

// https://www.w3.org/TR/turtle/#sec-grammar-grammar

statement { Directive | Triples '.' }                                           // [2]  statement    ::= directive | triples '.'
Directive { PrefixID | Base | SparqlPrefix | SparqlBase }                       // [3] 	directive    ::= prefixID | base | sparqlPrefix | sparqlBase
PrefixID { '@prefix' PN_PREFIX ':' IRIREF '.' }                                 // [4] 	prefixID     ::= '@prefix' PNAME_NS IRIREF '.'
Base { '@base' IRIREF '.' }                                                     // [5]  base         ::= '@base' IRIREF '.'
SparqlBase { base_token IRIREF  }                                               // [5s] sparqlBase   ::= "BASE" IRIREF
SparqlPrefix { prefix_token PN_PREFIX ':' IRIREF }                              // [6s] sparqlPrefix ::= "PREFIX" PNAME_NS IRIREF
Triples {                                                                       // [6] triples ::= subject predicateObjectList | blankNodePropertyList predicateObjectList?
  Subject predicateObjectList
  | BlankNodePropertyList predicateObjectList?
}
predicateObjectList { Verb objectList (';' (Verb objectList)?)* }               // [7] predicateObjectList ::= verb objectList (';' (verb objectList)?)*
objectList { Object Annotation? (',' Object Annotation?)* }                     // [8] objectList ::= object (',' object)*
Verb { predicate | 'a' }                                                        // [9]  verb         ::= predicate | 'a'
Subject { iri | blanknode_rule | Collection | QuotedTriple }                    // [10] subject      ::= iri | BlankNode | collection
predicate { iri }                                                               // [11] predicate    ::= iri
Object { iri | blanknode_rule | Collection | literal | BlankNodePropertyList    // [12] object       ::= iri | BlankNode | collection | blankNodePropertyList | literal
  | QuotedTriple }
literal { RDFLiteral | NumericLiteral | BooleanLiteral }                        // [13] literal  ::= RDFLiteral | NumericLiteral | BooleanLiteral
BlankNodePropertyList { '[' predicateObjectList ']' }                           // [14] BlankNodePropertyList 	::= 	'[' predicateObjectList ']'
Collection { '(' Object * ')' }                                                 // [15] collection ::= '(' object* ')'
RDFLiteral { string (langtag | '^^' iri )? }                                    // [128s] 	RDFLiteral 	::= 	String (LANGTAG | '^^' iri)?
NumericLiteral { integer | decimal | double }                                   // [16]     NumericLiteral  ::=   INTEGER | DECIMAL | DOUBLE
BooleanLiteral { 'true' | 'false' }                                             // [133s]   BooleanLiteral 	::= 	'true' | 'false'
QuotedTriple { '<<' QtSubject Verb QtObject '>>' }                              // [27t] 	quotedTriple 	::= 	'<<' qtSubject verb qtObject '>>'
QtSubject { iri | BlankNode | QuotedTriple }                                    // [28t] 	qtSubject 	::= 	iri | BlankNode | quotedTriple
QtObject { iri | BlankNode | literal | QuotedTriple }                           // [29t] 	qtObject 	::= 	iri | BlankNode | literal | quotedTriple
Annotation { '{|' predicateObjectList '|}' | '@{' predicateObjectList '}' }     // [30t] 	annotation 	::= 	'{|' predicateObjectList '|}'


Literal { string_literal_quote }
iri { IRIREF | PrefixedName }                                                   // [135s] 	iri 	::= 	IRIREF | PrefixedName
blanknode_rule { BlankNode | Anon }                                             // [137s] 	BlankNode 	::= 	BLANK_NODE_LABEL | ANON



// [136s] 	PrefixedName 	::= 	PNAME_LN | PNAME_NS
// PrefixedName { PNAME_LN }

PrefixedName { PN_PREFIX? ':' PN_LOCAL }



string { string_literal_quote }

@tokens {
  @precedence { 'a', base_token, prefix_token, 'true', 'false', PN_PREFIX, integer, decimal, double }

  IRIREF { '<' (![\u{00}-\u{20}<>"{}|^`\\] /* | UCHAR */ )* '>' }               // [18] 	IRIREF 	::= 	'<' ([^#x00-#x20<>"{}|^`\] | UCHAR)* '>'
  pname_ns { PN_PREFIX? ':' }                                                   // [139s] 	PNAME_NS 	::= 	PN_PREFIX? ':'
  pname_ln { pname_ns PN_LOCAL }                                                // [140s] 	PNAME_LN 	::= 	PNAME_NS PN_LOCAL
  BlankNode { '_:' (pn_chars_u | $[0-9]) ((pn_chars | '.')* pn_chars)? }        // [141s] 	BLANK_NODE_LABEL 	::= 	'_:' (PN_CHARS_U | [0-9]) ((PN_CHARS | '.')* PN_CHARS)?
  langtag { '@' $[a-zA-Z]+ ( '-' $[a-zA-Z0-9]+ )* }                             // [144s]   LANGTAG  ::= '@' [a-zA-Z]+ ('-' [a-zA-Z0-9]+)*
  integer { $[+-]? $[0-9]+ }                                                    // [19]     INTEGER  ::=  [+-]? [0-9]+
  decimal { $[+-]? $[0-9]* '.' $[0-9]+ }                                        // [20]     DECIMAL  ::=  [+-]? [0-9]* '.' [0-9]+
  double  {                                                                     // [21]     DOUBLE   ::=  [+-]? ([0-9]+ '.' [0-9]* EXPONENT | '.' [0-9]+ EXPONENT | [0-9]+ EXPONENT)
    $[+-]?
    ($[0-9]+ '.' $[0-9]* exponent | '.' $[0-9]+ exponent | $[0-9]+ exponent)
  }
  exponent { $[eE] $[+-]? $[0-9]+ }                                             // [154s]  EXPONENT  ::= 	[eE] [+-]? [0-9]+
  string_literal_quote { '"' $[a-zA-Z0-9 _+]* '"' }


  
  pn_chars_base {                                                               // [163s] 	PN_CHARS_BASE
    $[A-Z] | $[a-z]
    | $[\u{00C0}-\u{00D6}] | $[\u{00D8}-\u{00F6}] | $[\u{00F8}-\u{02FF}]
    | $[\u{0370}-\u{037D}] | $[\u{037F}-\u{1FFF}] | $[\u{200C}-\u{200D}]
    | $[\u{2070}-\u{218F}] | $[\u{2C00}-\u{2FEF}] | $[\u{3001}-\u{D7FF}]
    | $[\u{F900}-\u{FDCF}] | $[\u{FDF0}-\u{FFFD}] | $[\u{10000}-\u{EFFFF}]
  }
  pn_chars_u { pn_chars_base | '_' }                                            // [164s] 	PN_CHARS_U 	::= 	PN_CHARS_BASE | '_'
  pn_chars {                                                                    // [166s] 	PN_CHARS
    pn_chars_u | '-' | $[0-9]
    | '\u{00B7}' | $[\u{0300}-\u{036F}] | $[\u{203F}-\u{2040}]
  }


  "."

  LineComment { "#" ![\n]* }

  space { $[ \t\n\r]+ }

  PN_LOCAL { $[a-zA-Z0-9_]+ }

  PN_PREFIX { $[a-zA-Z] pn_chars_base* }

  


  Anon { '[' space? ']' }                                                       // [162s] 	ANON 	::= 	'[' WS* ']'

  // TODO: case insensitive tokens
  base_token { "BASE" | "base" }
  prefix_token { "PREFIX" | "prefix" }

  
}

@detectDelim
